// https://docs.gradle.org/current/userguide/custom_plugins.html
apply plugin: 'kotlin'
apply plugin: 'groovy' // for dsl tests
apply plugin: 'maven'

group = 'org.greenrobot'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://example.org/mvnrepo'
        credentials {
            username internalUser
            password internalPassword
        }
    }
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile project(':greendao-code-modifier')
    compileOnly 'com.android.tools.build:gradle:2.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

def genResourcesDir = new File(buildDir, 'src/generated/resources')
def versionFile = new File(genResourcesDir, 'org/greenrobot/greendao/gradle/version.properties')

sourceSets {
    main {
        resources {
            srcDir genResourcesDir
        }
    }
}

// puts artifact version to properties file inside resources
task generateVersionProperties {
    inputs.property 'version', project.version
    outputs.file versionFile

    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.withWriter {
            it.write """# Generated by gradle task generateVersionProperties
version=${project.version}
"""
        }
    }
}

tasks['compileKotlin'].dependsOn generateVersionProperties

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'greenDAO Gradle Plugin'
                description 'Gradle Plugin for greenDAO, the light and fast ORM for Android'
            }
        }
    }
}