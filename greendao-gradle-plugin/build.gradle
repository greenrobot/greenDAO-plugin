// https://docs.gradle.org/current/userguide/custom_plugins.html
apply plugin: 'kotlin'
apply plugin: 'groovy' // for dsl tests
apply plugin: 'maven'

group = 'org.greenrobot'
version = '0.1-SNAPSHOT'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile project(':greendao-code-modifier')
    compileOnly 'com.android.tools.build:gradle:2.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

uploadArchives {
    repositories {
        mavenDeployer {
        }
    }
}

def genSrcDir = new File(buildDir, 'src/generated/kotlin')
def versionFile = new File(genSrcDir, 'org/greenrobot/greendao/gradle/BuildConfig.kt')

sourceSets {
    main {
        kotlin {
            srcDir genSrcDir
        }
    }
}

// BuildConfig makes the artifact version available to code
task generateBuildConfig {
    inputs.property 'version', project.version
    outputs.file versionFile

    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.withWriter {
            it.write """/* Generated by gradle task */
package org.greenrobot.greendao.gradle

object BuildConfig {
    val version = "${project.version}"
}
"""
        }
    }
}

tasks['compileKotlin'].dependsOn generateBuildConfig