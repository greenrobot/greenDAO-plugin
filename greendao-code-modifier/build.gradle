apply plugin: 'kotlin'
apply plugin: 'maven'

group = 'org.greenrobot'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // use JDT Core packaged by the Eclipse Scout project, has the latest versions
    // requires special version resolution, see resolution strategy above
    compile 'org.eclipse.scout.sdk.deps:org.eclipse.jdt.core:3.12.0.v20160516-2131'

    compile 'org.greenrobot:greendao-api:3.2.0-SNAPSHOT'
    compile 'org.greenrobot:greendao-generator:3.2.0-SNAPSHOT'
    compile 'de.greenrobot:java-common:2.3.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'com.natpryce:hamkrest:1.1.0.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'README'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'README'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'greenDAO Code Modifier'
                description 'Code modifier for greenDAO, the light and fast ORM for Android'
            }
        }
    }
}
